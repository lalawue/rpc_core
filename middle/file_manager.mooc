--
-- Copyright (c) 2020 lalawue
--
-- This library is free software; you can redistribute it and/or modify it
-- under the terms of the MIT license. See LICENSE for details.
--

import FileSystem from "lfs"
import Zlib from "base.ffi_zlib"

_dummy_tbl = {}

struct FileManager {

    static fn mkdir(dir_path) {
        FileSystem.mkdir(dir_path)
    }

    static fn stat(path) {
        return FileSystem.attributes(path)
    }

    -- list dir files, tbl_modes should be { "files" = true, "directory" = true }
    static fn listDir(dir_path, tbl_modes) {
        if type(dir_path) ~= "string" or type(tbl_modes) ~= "table" {
            return _dummy_tbl
        }
        attr = FileSystem.attributes(dir_path)
        if type(attr) ~= "table" or attr.mode ~= "directory" {
            return _dummy_tbl
        }
        tbl = {}
        for fname in FileSystem.dir(dir_path) {
            attr = FileSystem.attributes(dir_path .. "/" .. fname)
            if attr and tbl_modes[attr.mode] {
                tbl[#tbl + 1] = fname
            }
        }
        return tbl
    }

    -- save data to path
    static fn saveFile(file_path, data) {
        f = io.open(file_path, "wb")
        if f {
            f:write(data)
            f:close()
            return true
        }
        return false
    }

    static fn readFile(file_path) {
        f = io.open(file_path, "rb")
        if f {
            data = f:read("*a")
            f:close()
            return data
        }
        return nil
    }

    static fn appendFile(file_path, data) {
        f = io.open(file_path, "a+")
        if f {
            f:write(data)
            f:close()
            return true
        }
        return false
    }

    static fn removeFile(file_path) {
        os.remove(file_path)
    }

    static fn renameFile(oldname, newname) {
        os.rename(oldname, newname)
    }

    static fn inflate(input_content) {
        if type(input_content) ~= "string" {
            return nil
        }
        _input = { buf_size in
            min_len = math.min(buf_size, input_content:len())
            if min_len > 0 {
                data = input_content:sub(1, min_len)
                input_content = input_content:sub(1 + min_len)
                return data
            }
        }
        tbl = {}
        _ouput = { data in
            tbl[#tbl + 1] = data
        }
        success, err_msg = Zlib.inflateGzip(_input, _ouput)
        if not success {
            return table.concat(tbl), err_msg
        }
        return table.concat(tbl)
    }

    static fn deflate(input_content) {
        if type(input_content) ~= "string" {
            return nil
        }
        _input = { buf_size in
            min_len = math.min(buf_size, input_content:len())
            if min_len > 0 {
                data = input_content:sub(1, min_len)
                input_content = input_content:sub(1 + min_len)
                return data
            }
        }
        tbl = {}
        _ouput = { data in
            tbl[#tbl + 1] = data
        }
        success, err_msg = Zlib.deflateGzip(_input, _ouput)
        if not success {
            return table.concat(tbl), err_msg
        }
        return table.concat(tbl)
    }
}

return FileManager
