--
-- Copyright (c) 2020 lalawue
--
-- This library is free software; you can redistribute it and/or modify it
-- under the terms of the MIT license. See LICENSE for details.
--

--
-- raw tcp stated stream, interface should be same as tcp_ssl
--

import FFI from "ffi"
import NetCore from "ffi-mnet"
Log = require("middle.logger")("[TcpRaw]", "error")

FFI.cdef([[
    enum {
        SSL_VERIFY_NONE = 0x00,
        SSL_VERIFY_PEER = 0x01,
        SSL_VERIFY_FAIL_IF_NO_PEER_CERT = 0x02,
        SSL_VERIFY_CLIENT_ONCE = 0x04,
        SSL_VERIFY_POST_HANDSHAKE = 0x08,
    };
    enum {
        SSL_FILETYPE_PEM = 1,
        SSL_FILETYPE_ASN1 = 2,
    };
    void OPENSSL_init_ssl(uint64_t, void*);
    void* TLS_method(void);
    void* SSL_CTX_new(void *);
    void SSL_CTX_set_verify(void*, int, void*);
    int SSL_CTX_load_verify_locations(void*, const char*, const char*);
    int SSL_CTX_use_certificate_file(void*, const char*, int);
    int SSL_CTX_use_PrivateKey_file(void*, const char *, int);
    int SSL_CTX_check_private_key(void*);
]])

_has_init = false

struct ChannRaw {
    _options = false
    _chann = false
    _callback = false
    _openssl = false

    fn init(options) {
        if not _has_init {
            NetCore.init()
        }
        self._options = options
        if options == "https" {
            self:installOpenSSL()
            self._chann = NetCore.openChann("tls")
        } else {
            self._chann = NetCore.openChann("tcp")
        }
    }

    fn installOpenSSL() {
        guard self._openssl == false else {
            return
        }
        openssl = FFI.load("libssl.so.1.1")
        openssl.OPENSSL_init_ssl(0, nil)
        ctx = openssl.SSL_CTX_new(openssl.TLS_method())
        openssl.SSL_CTX_set_verify(ctx, openssl.SSL_VERIFY_NONE, nil)
        --openssl.SSL_CTX_load_verify_locations(ctx, "examples/openssl/ca.crt", nil)
        --openssl.SSL_CTX_use_certificate_file(ctx, "examples/openssl/server.crt", openssl.SSL_FILETYPE_PEM)
        --openssl.SSL_CTX_use_PrivateKey_file(ctx, "examples/openssl/server.key", openssl.SSL_FILETYPE_PEM);
        --assert(openssl.SSL_CTX_check_private_key(ctx) == 1)
        NetCore.extConfig("tls", ctx)
        self._openssl = openssl
    }

    fn closeChann() {
        if self._chann {
            self._chann:close()
            self._chann = false
        }
    }

    fn connectAddr(ipv4, port) {
        if self._chann and self._chann:state() ~= "state_connected" {
            self._chann:connect(ipv4, port)
            return true
        } else {
            Log:error("failed to connect '%s:%d', %s", ipv4, port, self._chann)
            return false
        }
    }

    -- callback params should be (self, event_name, accept_chann, c_msg)
    fn setCallback(callback) {
        if not callback {
            Log:error("invalid callback param")
            return
        }
        self._callback = callback
        self._chann:setCallback({ chann, event_name, accept_chann, c_msg in
            switch event_name {
            case "event_connected":
                self._callback(self, event_name, accept_chann, c_msg)
            case "event_recv":
                self._callback(self, event_name, nil, c_msg)
            case "event_send":
                self._callback(self, event_name, nil, c_msg)
            case "event_disconnect":
                self._callback(self, event_name, nil, c_msg)
            case "event_timer":
                self._callback(self, event_name, nil, c_msg)
            case "event_accept":
                chann_raw = setmetatable({}, ChannRaw)
                chann_raw._chann = accept_chann
                self._callback(self, event_name, chann_raw, c_msg)
            }
        })
    }

    fn send(data) {
        if self._chann and self._chann:state() == "state_connected" {
            return self._chann:send(data)
        }
    }

    fn recv() {
        if self._chann and self._chann:state() == "state_connected" {
            return self._chann:recv()
        }
    }

    fn setEventTimer(timeout_second) {
        if self._chann {
            self._chann:activeEvent("event_timer", timeout_second * 1000000)
        }
    }

    fn onLoopEvent() {
        return false
    }
}

return ChannRaw
